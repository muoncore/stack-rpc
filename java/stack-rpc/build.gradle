apply plugin:"groovy"

repositories {
    maven {
        url "http://artifactory.cloud.daviddawson.me/artifactory/muon"
    }
}

dependencies {
    compile "io.muoncore:muon-core:7.4.1"
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourceJar {
                classifier "sources"
            }
            artifact packageJavadoc
            pom.withXml {
                asNode().appendNode('name', "Muon RPC API")
                asNode().appendNode('description', 'Muon is a toolkit for building highly portable, polyglot, reactive APIs. This implements the RPC class of APIs, across any Muon transport')
                asNode().appendNode('url', 'http://muoncore.io')
                def license = asNode().appendNode("licenses").appendNode("license")
                license.appendNode("name", "The GNU Lesser General Public License, Version 3.0")
                license.appendNode("url", "http://www.gnu.org/licenses/lgpl-3.0.txt")
                license.appendNode("distribution", "repo")

                asNode().appendNode("scm").appendNode("url", "https://github.com/muoncore/stack-rpc")

                def developers = asNode().appendNode("developers")
                def dev = developers.appendNode("developer")
                dev.appendNode("name", "Muon Developers")
                dev.appendNode("email", "info@muoncore.io")
                dev.appendNode("organization", "Muon Project")
                dev.appendNode("organizationUrl", "http://muoncore.io")

                asNode().packaging[0].value = 'jar'
                asNode().dependencies.'*'.each{
                    it.scope[0].value="compile"
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
